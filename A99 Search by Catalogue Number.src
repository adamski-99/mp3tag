# ###################################################################
# Mp3tag Tag Source for Discogs API (Official)
# 
# v1.0 (2014-09-20)
#
# Search by Catalogue Number
# 
# This file should be stored in your tag sources sources directory
# %APPDATA%\Mp3tag\data\sources
# and requires Mp3tag v2.64 or above.
#
# ###################################################################
Debug "on" "debug.out"

[Name]=Discogs.com
[BasedOn]=www.discogs.com
[IndexUrl]=https://api.discogs.com/database/search?type=release
#[AlbumUrl]=https://api.discogs.com/releases/
[WordSeperator]=%20
[IndexFormat]=%Artist%|%Album%|%_url%|%Format%|%Year%|%Label%|%Country%|%_preview%
[SearchBy]=Catalogue Number||%catno%||&catno=%s
[UserAgent]=1
[Encoding]=url-utf-8

[ParserScriptIndex]=...
Debug "on" "debug_1.out"
DebugWriteInput "debug-input.out"
# ###################################################################
#					I  N  D  E  X
# ###################################################################
json "ON"

json_foreach "results"

	json_select "type"

	if "release"	
	
		# Artist
		json_select "title"
		sayregexp ".+?(?= -.*)"
		say "|" 

		# Album
		sayregexp "(?<= - ).*"
		say "|"

		# URL
		json_select "resource_url"
		sayrest
		say "|"

		# Format
		json_select_array "format" 1
		sayrest
		say "|"

		# Year
		json_select "year"
		sayrest
		say "|"

		# Label
		json_select_array "label" 1
		sayrest
		say "|"

		# Country
		json_select "country"
		sayrest
		say "|"

		# Preview
		say "http://www.discogs.com/release/"
		json_select "id"
		sayrest

		saynewline

	endif

json_foreach_end


[ParserScriptAlbum]=...
Debug "on" "debug_3.out"
DebugWriteInput "debug-input.out"
# ###################################################################
#					A  L  B  U  M
# ###################################################################
json "ON"

# Album
outputto "ALBUM"
json_select "title"
sayrest

# Discogs_Release_ID
outputto "DISCOGS_RELEASE_ID"
json_select "id"
sayrest

# Coverurl
outputto "coverurl"
json_foreach "images"
	json_select "type"
	if "primary"
		json_select "uri"
		sayrest
	endif
json_foreach_end

# Catalog Number
outputto "CATALOGID"
json_select_many "labels" "catno" ", "
sayrest

# Publisher
outputto "PUBLISHER"
json_select_many "labels" "name" ", "
sayrest

# Media Type
outputto "MEDIATYPE"
json_select_many "formats" "name" ", "
sayrest
json_select_many "formats" "descriptions" ", "
ifnot ""
	say " "
endif
sayrest

# Genre
outputto "GENRE"
json_select_array "genres" -1 ", "
sayrest

# Style
outputto "STYLE"
json_select_array "styles" -1 ", "
sayrest

# Country
outputto "COUNTRY"
json_select "country"
sayrest

# Year
outputto "YEAR"
json_select "year"
sayrest

# Www
outputto "WWW"
json_select "uri"
sayrest

# Artist or Albumartist
json_foreach "tracklist"
	json_select_many "artists" "name" ", "
json_foreach_end

ifnot ""
	## Various Artists
	outputto "ALBUMARTIST"
else
	## Single Artist
	outputto "ARTIST"
endif

json_foreach "artists"
	json_select "anv"
	ifnot ""
		sayrest
	else
		json_select "name"
		sayrest
	endif

	json_select "join"
	ifnot ""
		say " "
		sayrest
		say " "
	endif
json_foreach_end

# Tracks
json_foreach "tracklist"
	json_select "type_"
	if "track"
		outputto "TRACKS"
		json_select "title"
		sayrest
		say "|"

		outputto "_LENGTH"
		json_select "duration"
		sayrest
		say "|"

		json_select_many "artists" "name" ", "
		ifnot ""
			outputto "ARTIST"
			json_foreach "artists"
				json_select "anv"
				ifnot ""
					sayrest
				else
					json_select "name"
					sayrest
				endif

				json_select "join"
				ifnot ""
					say " "
					sayrest
					say " "
				endif
			json_foreach_end
			say "|"
		endif
	endif
json_foreach_end